prototype(Sitegeist.NeosTemplate.Spectral:Content.Section.BannerLanding) < prototype(Neos.Neos:ContentComponent) {

    title = Neos.Neos:Editable {
        property = 'title'
        block = false
    }

    text = Neos.Neos:Editable {
        property = 'text'
        block = false
    }

    buttons = Neos.Neos:ContentCollectionRenderer {
        nodePath = "."
    }

    buttonLabel = ${q(node).property('linkLabel') || Translation.translate('Sitegeist.NeosTemplate.Spectral:Main:banner.buttonLabel')}

    # this is used to remove zhe current uri from
    # when an anchor on the same document is used
    @context.currentUri = Neos.Neos:NodeUri {
        node = ${documentNode}
    }

    buttonLink = ${q(node).property('link')}
    buttonLink.@process.convertUris = Neos.Neos:ConvertUris
    buttonLink.@process.replaceCurrentUri = ${String.replace(value, currentUri + '#', '#')}
    buttonLink.@if.isLive = ${node.context.live}

    renderer = afx`
        <section class="banner">
            <div class="inner">
                <h2>{props.title}</h2>
                <p>{props.text}</p>
                <ul class="actions special">
                    {props.buttons}
                </ul>
            </div>
            <a href={props.buttonLink} class="more scrolly">{props.buttonLabel}</a>
        </section>
    `
}
